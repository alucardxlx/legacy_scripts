include "include/modules";
include "include/gumps";

var amount, objtype, res;

program textcmd_createstack( who, params )
	if( params )
		params := SplitWords( params );
		objtype := params[1];
		amount := CInt( params[2] );
		DoCreate( who, objtype, amount );
	else
		var gump_ret := DoGump( who );
		if( gump_ret[1] == 1 )
			DoCreate( who, gump_ret[2], gump_ret[3] );
		elseif( gump_ret[1] == 3 )
			var selection := SelectMenuItem2( who, "ItemCreation" );
			amount := RequestInput( who, gump_ret[2], "How many would you like?" );
			DoCreate( who, CInt(selection.objtype), CInt(amount) ); 
		endif
	endif
endprogram

function DoGump( who )
	GFSetRef( 50, 50 );
	GFPage( 0 );
	GFNoDispose();
	GFResizePic( 0, 0, BKGND_GOLDTRIM, 210, 220 );
	GFGumpPic( 10, 71, 0x462 );
	GFGumpPic( 10, 116, 0x462 );
	GFTextMid( 0, 15, 210, 4, "Enter Create Variables" );
	GFTextLine( 10, 50, 4, "Enter Objtype" );
	GFTextLine( 10, 95, 4, "Enter Amount" );
	GFTextLine( 10, 180, 4, "Or use the create menu" );
	GFButtonID( 50, 140, 0x992, 0x993, 1 );
	GFButtonID( 120, 140, 0x995, 0x996, 1 );
	GFButtonID( 180, 186, 0x939, 0x938, 1 );
	objtype := GFTextEntry( 11, 70, 175, 16, 4 );
	amount := GFTextEntry( 11, 115, 175, 16, 4 );
	res := GFSendGump( who );
	objtype := GFExtractData( objtype.keyid, res );
	amount := GFExtractData( amount.keyid, res );
	var params := { CInt(res[0]-1024), objtype, CInt(amount) };
	return params;
endfunction

function DoCreate( who, objct, amnt )
	if( !CInt(objct) )
		objct := GetObjtypeByName( objct );
	endif
	if ( !CInt(amnt) )
		amnt := 1;
	endif
	var item := CreateItemInBackpack( who, CInt(objct), amnt );
	if( item )
		PrintTextAbovePrivate( who, "Created " + item.amount + " of " + item.desc + " in my backpack.", who );
	else
		PrintTextAbovePrivate( who, "Couldn't create that: " + item.errortext, who );
	endif
endfunction