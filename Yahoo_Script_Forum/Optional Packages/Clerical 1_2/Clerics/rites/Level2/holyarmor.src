use uo;
use util;

include "../../cleric";

program holyarmor(params)
	var who := params[1];
	var info := params[2];
	
	var devotion := GetObjProperty(who, "Devotion");
	var piety := DevotionPiety(devotion, who);
	var pietyboost := cint(GetConcentratingClerics(who, 5)/2);
	if (pietyboost + piety <= 7)
		piety := piety + pietyboost;
	endif
	
	ReleaseScript(info.pid);
	
	var hand2 := GetEquipmentByLayer(who, 2);
	var holyshield := CreateItemInBackPack(who, 0xa364, 1);
	holyshield.movable := 1;
	MoveItemToContainer(hand2, who.backpack);
	
	holyshield.graphic := ShieldGraphic(devotion);
	
	EquipItem(who, holyshield);
	holyshield.movable := 0;
	PlaySoundEffect(who, 489);
	sleep(piety*60);
	
	DestroyItem(holyshield);
	EquipItem(who, hand2);
endprogram	

function ShieldGraphic(devotion)
	case (devotion)
		"JusticeGod": return 7108;
		"ChaosGod": return 7107;
		"ThiefGod": return 7035;
		"NatureGod": return 7032;
		"Wargod": return 7026;
		default: return 7030;
	endcase
endfunction