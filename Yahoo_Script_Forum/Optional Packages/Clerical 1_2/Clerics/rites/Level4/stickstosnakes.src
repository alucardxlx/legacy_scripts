use uo;
use os;

include "../../cleric";

var staffs := {5113, 3568, 3722};

program dispelmagic(params)
	var who := params[1];
	var info := params[2];
	
	var devotion := GetObjProperty(who, "Devotion");
	var piety := DevotionPiety(devotion, who);
	var pietyboost := cint(GetConcentratingClerics(who, 5)/2);
	if (pietyboost + piety <= 7)
		piety := piety + pietyboost;
	endif
	
	var staff := ClericalTarget(who, info.npcstaffet, "Select a staff in your pack");
	var realgraphic := staff.graphic;
	
	if (staff.container != who.backpack)
		SendSysMessage(who, "Cancelled");
		return;
	elseif (staff.graphic in staffs);
	else
		SendSysMessage(who, "Cancelled");
		return;
	endif
	
	ReleaseScript(info.pid);
	
	var slave := CreateNPCFromTemplate("giantserpent", who.x, who.y, who.z);
	slave.color := staff.color;
	
	MoveItemToContainer(staff, who.backpack);
	staff.movable := 0;
	staff.graphic := 0;
	
	SetObjProperty(slave, "master" , who.serial);
	SetObjProperty(slave, "summoned", 1);
	SetObjProperty(slave, "guardkill", 1);
	slave.SetMaster(who);
	slave.script := "tamed";
	RestartScript(slave);
	
	var slavex, slavey, slavez;
	var count := 0;
	while (count < piety * 90 && slave.npctemplate)
		count := count+1;
		slavex := slave.x;
		slavey := slave.y;
		slavez := slave.z;
		sleep(1);
	endwhile
	ApplyRawDamage(slave, 10000);

	staff.movable := 1;
	staff.graphic := realgraphic;
	
	MoveItemToLocation(staff, slavex, slavey, slavez);
endprogram