use uo;
use os;
use util;

include "include/yesno2";
include "../../cleric";

program malediction(params)
	var who := params[1];
	var info := params[2];
	
	var devotion := GetObjProperty(who, "Devotion");
	var piety := DevotionPiety(devotion, who);
	var pietyboost := cint(GetConcentratingClerics(who, 5)/2);
	if (pietyboost + piety <= 7)
		piety := piety + pietyboost;
	endif
	
	var targ := ClericalTarget(who, info.npctarget, "Select the one who has mocked your god!", TGTOPT_CHECK_LOS + TGTOPT_HELPFUL);
	
	if (!targ.ip && ! targ.npctemplate)
		SendSysMessage(who, "Cancelled");
		return;
	elseif (!yesno(who, "Only use this as a last resort!", "Yes this person can be taught no other way", "No, I will find other ways to handle it"))
		SendSysMessage(who, "Cancelled");
		return;
	endif
	
	PrintTextAbove(targ, targ.name + " has been cursed by the gods!");
	PlayObjectCenteredEffect(targ, 0x374a, 20, 20);
	PlaySoundEffect(targ, 482);
	
	var dice := RandomDiceRoll("1d7");
	
	if (dice == 1)
		// Stat Loss
		StatLoss(targ);
	elseif (dice >=2 && dice <= 7)
		// Equipment tosser
		equipmentdrop(targ);
	endif
	
endprogram

function StatLoss(mobile)
	var dice := RandomDiceRoll("1d3");
	if (dice == 1)
		PrintTextAbove(mobile, "*" + mobile.name + " feels weaker*");
		SendSysMessage(mobile, "You permanently lost 2 strength");
		mobile.setstr(mobile.strength-2);
	elseif (dice == 2)
		PrintTextAbove(mobile, "*" + mobile.name + " feels less nimble*");
		SendSysMessage(mobile, "You permanently lost 2 dexterity");
		mobile.setdex(mobile.dexterity-2);
	elseif (dice == 3)
		PrintTextAbove(mobile, "*" + mobile.name + " feels less intelligent*");
		SendSysMessage(mobile, "You permanently lost 2 intelligence");
		mobile.setint(mobile.intelligence-2);
	endif
endfunction

function EquipmentDrop(mobile)
	foreach item in ListEquippedItems(mobile);
		if (item != mobile.backpack)
			MoveItem(mobile, item);
		elseif (item == mobile.backpack)
			foreach thing in item // Backpack
				if (thing.container == item)
					MoveItem(mobile, thing);
				endif
			endforeach
		endif
	endforeach
	PrintTextAbove(mobile, "*" + mobile.name + " feels colder*");
endfunction

function MoveItem(mobile, item)
	var newx, newy;
	DO
		BEGIN
			newx := randomint(10)-5;
			newy := randomint(10)-5;
			
			newx := newx+mobile.x;
			newy := newy+mobile.y;
		END
	while(!CheckLOSAt(mobile, newx, newy, getworldheight(newx, newy)));
	MoveItemToLocation(item, newx, newy, getworldheight(newx, newy), MOVEITEM_FORCELOCATION);
endfunction