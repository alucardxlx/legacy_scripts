use uo;
use os;
use util;

include "../../cleric";

program bladebarrier(params)
	var who := params[1];
	var info := params[2];
	
	var devotion := GetObjProperty(who, "Devotion");
	var piety := DevotionPiety(devotion, who);
	var pietyboost := cint(GetConcentratingClerics(who, 5)/2);
	if (pietyboost + piety <= 7)
		piety := piety + pietyboost;
	endif
	
	var targ := clericaltargetcoordinates(who, info.npctarget);
	if (!targ || !CheckLOSAt(who, targ.x, targ.y, targ.z))
		SendSysMessage(who, "Cancelled");
		return;
	endif
	
	var blades := { };
	case (getfacing(who, targ.x, targ.y, targ.z))
		"north" : blades:= CreateNorthSouthField(who, targ.x, targ.y, targ.z, piety);
		"south" : blades:= CreateNorthSouthField(who, targ.x, targ.y, targ.z, piety);
		"east"  : blades:= CreateEastWestField(who, targ.x, targ.y, targ.z, piety);
		"west"  : blades:= CreateEastWestField(who, targ.x, targ.y, targ.z, piety);
		default:
			case (randomint(2))
				0: blades:= CreateNorthSouthField(who, targ.x, targ.y, targ.z, piety);
				1: blades:= CreateEastWestField(who, targ.x, targ.y, targ.z, piety);
			endcase	
	endcase
	
	ReleaseScript(info.pid);
	
	//AI will handle their destruction
endprogram

Function CreateNorthSouthField(who,xcoord,ycoord,zcoord,length := 5)
	var i;
	var slave;
	var blades := { };	
	for ( i := xcoord - 2; i <= xcoord - 2 + length; i := i + 1)
		slave := CreateNPCFromTemplate("bladewall",i,ycoord,zcoord);
		SetObjProperty(slave, "guardkill", 1);
		if(!start_script("controlscripts/bladebarriercontrol", slave))
			slave.frozen := 1;
			PrintTextAbove(slave, "Error: Cant start blade control");
		endif
		blades.append(slave);
	endfor
	
	return blades;
endfunction

Function CreateEastWestField(who,xcoord,ycoord,zcoord,length := 5)
	var i;
	var slave;
	var blades := { };
	for ( i := ycoord - 2; i <= ycoord - 2 + length; i := i + 1)
		slave := CreateNPCFromTemplate("bladewall",xcoord,i,zcoord);
		SetObjProperty(slave, "guardkill", 1);
		if(!start_script("controlscripts/bladebarriercontrol", slave))
			slave.frozen := 1;
			PrintTextAbove(slave, "Error: Cant start blade control");
		endif
		blades.append(slave);
	endfor
	return blades;
endfunction