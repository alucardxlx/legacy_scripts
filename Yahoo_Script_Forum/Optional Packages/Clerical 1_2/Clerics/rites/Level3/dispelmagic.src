use uo;
use os;

include "../../cleric";

var glyphs := {GLYPH_OF_WARDING, SYMBOL_OF_PAIN};

program dispelmagic(params)
	var who := params[1];
	var info := params[2];
	
	var devotion := GetObjProperty(who, "Devotion");
	var piety := DevotionPiety(devotion, who);
	var pietyboost := cint(GetConcentratingClerics(who, 5)/2);
	if (pietyboost + piety <= 7)
		piety := piety + pietyboost;
	endif
	
	killsummoned(who, piety);
	potions(who, piety);
	breakglyphs(who, piety);
endprogram

function killsummoned(who, piety)
	foreach mobile in ListMobilesNearLocationEX(who.x, who.y, who.z, piety*2, LISTEX_FLAG_NORMAL + LISTEX_FLAG_HIDDEN)
		if (CheckLineOfSight(who, mobile))
			if(GetObjProperty(mobile, "summoned") && mobile.npctemplate)
				PlaySoundEffect(mobile, 531);
				PrintTextAbove(mobile, "*" + mobile.name + "'s magic essence fades*");
				sleepms(800);
				ApplyRawDamage(mobile, 10000);				
			endif
		endif
	endforeach
endfunction

function potions(who, piety)
	var config := ReadConfigFile(":alchemy:itemdesc");	
	foreach mobile in ListMobilesNearLocationEX(who.x, who.y, who.z, piety*2, LISTEX_FLAG_NORMAL + LISTEX_FLAG_HIDDEN)
		if (CheckLineOfSight(who, mobile))
			foreach item in enumerateitemsincontainer(mobile.backpack.serial)
				if (config[item.objtype].idname)
					PrintTextAbove(mobile, "*potions shatter in " + mobile.name + "'s backpack*");
					PlaySoundEffect(mobile, 63);
					DestroyItem(item);
				endif
			endforeach
		endif
	endforeach
	
	foreach item in ListItemsNearLocation(who.x, who.y, who.z, piety*2)
		if (config[item.objtype].idname)
			if (item.amount > 1)
				PrintTextAbove(item, item.desc + " shatter");
			else
				PrintTextAbove(item, item.desc + " shatters");
			endif
			PlaySoundEffect(item, 63);
			sleepms(500);
			DestroyItem(item);
		endif
	endforeach
endfunction

function breakglyphs(who, piety)
	foreach item in ListItemsNearLocation(who.x, who.y, who.z, piety)
		if (item.objtype in glyphs)
			var power := cint(GetObjProperty(item, "power"));
			if (CheckSkill(who, CLERIC_SKILL, (power*10), (power*2)) > 0)
				PrintTextAbove(item, "*" + item.desc + " crumbles*");
				PlaySoundEffect(item, 531);
				sleepms(600);
				DestroyItem(item);
			else
				SendSysMessage(who, "You are unable to crack the power of " + item.desc);
			endif
		endif
	endforeach
endfunction