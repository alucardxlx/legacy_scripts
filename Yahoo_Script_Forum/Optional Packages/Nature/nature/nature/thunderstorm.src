use uo;
use os;
use util;

include "include/client";
include "nature";

const SOUND_EFFECT_THUNDER:=  0x002a;

program LightningEffect()

var daymoon := GetGlobalProperty("Nature");
var thundelay;

while (daymoon[CURRENTWEATHER] = 5)
	daymoon := GetGlobalProperty("Nature");
	SetRegionLightLevel("Background", 0);
	foreach who in EnumerateOnlineCharacters()
		If (who.x <= 5119)
			If (RandomInt(15) >= 14)
				DoBolt(who);
			EndIf
		EndIf
	endforeach
	sleepms(500);
	SetRegionLightLevel("Background", cint(daymoon[CURRENTLIGHT]));
	thundelay := RandomInt(5);
	sleep(thundelay);		
	foreach who in EnumerateOnlineCharacters()
		If (who.x <= 5119)
			PlaySoundEffect(who, SOUND_EFFECT_THUNDER);
		EndIf
	EraseObjProperty(who, "#heardbolt");
	endforeach
	var randompause := Cint(RandomInt(120));
	sleep(randompause);
endwhile

endprogram

function DoBolt(who)

If (who.multi)
	return;
endif
var xpos := who.x;
var ypos := who.y;
var xrand := RandomInt(20);
var yrand := RandomInt(20);
xpos := ((xpos - xrand) + 10);
ypos := ((ypos - yrand) + 10);
var zpos := GetMapInfo( xpos, ypos ).z;
var mob := CreateNpcFromTemplate( "Rabbit", xpos, ypos, zpos);
mob.cmdlevel := 2;
mob.concealed := 1;
setobjproperty(mob, "guardkill", 1);
PlayLightningBoltEffect( mob );
PlaySoundEffect(who, SOUND_EFFECT_THUNDER);
SetObjProperty(who, "#heardbolt", 1);
MoveCharacterToLocation(mob,0,0,0,MOVECHAR_FORCELOCATION);
Damage( mob, mob.hp+2 );

endfunction
